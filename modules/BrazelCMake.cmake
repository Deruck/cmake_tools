function(cc_library NAME)
  set(options STATIC SHARED)
  set(oneValueArgs "")
  set(multiValueArgs SRCS DEPS INCLUDE_DIRS)
  cmake_parse_arguments(ARG "${options}" "${oneValueArgs}" "${multiValueArgs}" ${ARGN})

  if(ARG_STATIC)
    add_library(${NAME} STATIC ${ARG_SRCS})
  elseif(ARG_SHARED)
    add_library(${NAME} SHARED ${ARG_SRCS})
  else()
    message(FATAL_ERROR "You must specify either STATIC or SHARED for cc_library")
  endif()

  if(ARG_DEPS)
    add_dependencies(${NAME} ${ARG_DEPS})
    target_link_libraries(${NAME} PRIVATE ${ARG_DEPS})
  endif()
  if (ARG_INCLUDE_DIRS)
    target_include_directories(${NAME} PUBLIC ${ARG_INCLUDE_DIRS})
  endif()
endfunction()

function(cc_binary NAME)
  set(oneValueArgs "")
  set(multiValueArgs SRCS DEPS)
  cmake_parse_arguments(ARG "${options}" "${oneValueArgs}" "${multiValueArgs}" ${ARGN})

  add_executable(${NAME} ${ARG_SRCS})

  if(ARG_DEPS)
    add_dependencies(${NAME} ${ARG_DEPS})
    target_link_libraries(${NAME} PRIVATE ${ARG_DEPS})
  endif()
endfunction()

function(cc_test NAME)
  set(options GTEST)
  set(oneValueArgs "")
  set(multiValueArgs SRCS DEPS)
  cmake_parse_arguments(ARG "${options}" "${oneValueArgs}" "${multiValueArgs}" ${ARGN})

  add_executable(${NAME} ${ARG_SRCS})

  if(ARG_DEPS)
    add_dependencies(${NAME} ${ARG_DEPS})
    target_link_libraries(${NAME} PRIVATE ${ARG_DEPS})
  endif()

  if(ARG_GTEST)
    include(GoogleTest)
    target_link_libraries(${NAME} PRIVATE GTest::gtest_main)
    gtest_discover_tests(${NAME})
  else()
    add_test(NAME ${NAME} COMMAND ${NAME})
  endif()
endfunction()