function(cc_library NAME)
  set(options STATIC SHARED INTERFACE)
  set(oneValueArgs NS)
  set(multiValueArgs SRCS DEPS INCLUDE_DIRS)
  cmake_parse_arguments(ARG "${options}" "${oneValueArgs}" "${multiValueArgs}" ${ARGN})
  if(ARG_NS)
    set(ORI_NAME ${NAME})
    string(REPLACE "::" "_" NS_PREFIX "${ARG_NS}")
    set(NAME ${NS_PREFIX}__${NAME})
  endif()

  if(ARG_STATIC)
    add_library(${NAME} STATIC ${ARG_SRCS})
  elseif(ARG_SHARED)
    add_library(${NAME} SHARED ${ARG_SRCS})
  elseif(ARG_INTERFACE AND ARG_INCLUDE_DIRS)
    add_library(${NAME} INTERFACE)
    target_include_directories(${NAME} INTERFACE ${ARG_INCLUDE_DIRS})
  else()
    message(FATAL_ERROR "You must specify either STATIC, SHARED or INTERFACE for cc_library")
  endif()

  if(ARG_DEPS)
    add_dependencies(${NAME} ${ARG_DEPS})
    target_link_libraries(${NAME} PRIVATE ${ARG_DEPS})
  endif()
  if (ARG_INCLUDE_DIRS AND NOT ARG_INTERFACE)
    target_include_directories(${NAME} PUBLIC ${ARG_INCLUDE_DIRS})
  endif()
  if(ARG_NS)
    add_library(${ARG_NS}::${ORI_NAME} ALIAS ${NAME})
  endif()
endfunction()

function(cc_binary NAME)
  set(oneValueArgs "")
  set(multiValueArgs SRCS DEPS)
  cmake_parse_arguments(ARG "${options}" "${oneValueArgs}" "${multiValueArgs}" ${ARGN})

  add_executable(${NAME} ${ARG_SRCS})

  if(ARG_DEPS)
    add_dependencies(${NAME} ${ARG_DEPS})
    target_link_libraries(${NAME} PRIVATE ${ARG_DEPS})
  endif()
endfunction()

function(cc_test NAME)
  set(options GTEST)
  set(oneValueArgs NS)
  set(multiValueArgs SRCS DEPS)
  cmake_parse_arguments(ARG "${options}" "${oneValueArgs}" "${multiValueArgs}" ${ARGN})
  if(ARG_NS)
    set(NAME ${ARG_NS}.${NAME})
  endif()

  add_executable(${NAME} ${ARG_SRCS})

  if(ARG_DEPS)
    add_dependencies(${NAME} ${ARG_DEPS})
    target_link_libraries(${NAME} PRIVATE ${ARG_DEPS})
  endif()

  if(ARG_GTEST)
    include(GoogleTest)
    target_link_libraries(${NAME} PRIVATE GTest::gtest_main)
    gtest_discover_tests(${NAME})
  else()
    add_test(NAME ${NAME} COMMAND ${NAME})
  endif()
endfunction()